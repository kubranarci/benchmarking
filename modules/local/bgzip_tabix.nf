process BGZIP_TABIX {
    tag "$meta.id"
    label 'process_single'

    conda ""
    container "${ workflow.containerEngine == 'singularity' && !task.ext.singularity_pull_docker_container ?
        'https://depot.galaxyproject.org/singularity/tabix:1.11--hdfd78af_0' :
        'biocontainers/tabix:1.11--hdfd78af_0' }"

    input:
    tuple val(meta),path(vcf)

    output:
    tuple val(meta),path("*.vcf.gz"), path("*.tbi"), emit: gz_tbi
    path  "versions.yml"                           , emit: versions

    when:
    task.ext.when == null || task.ext.when

    script:
    def args  = task.ext.args ?: ''
    def args2 = task.ext.args2 ?: ''
    def zipname  = vcf.getBaseName() + ".temp.vcf.gz"

    if (vcf.getExtension() == "gz"){
    """
    cp $vcf $zipname
    tabix -p vcf $zipname

    cat <<-END_VERSIONS > versions.yml
    "${task.process}":
        tabix: \$(echo \$(tabix -h 2>&1) | sed 's/^.*Version: //; s/ .*\$//')
    END_VERSIONS
    """
    }
    else{
    """
    bgzip ${args2} --threads ${task.cpus} -c $vcf > $zipname
    tabix -p vcf $zipname

    cat <<-END_VERSIONS > versions.yml
    "${task.process}":
        tabix: \$(echo \$(tabix -h 2>&1) | sed 's/^.*Version: //; s/ .*\$//')
    END_VERSIONS
    """
    }
}
